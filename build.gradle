buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name 'jboss-nexus'
			url "http://repository.jboss.org/nexus/content/groups/public/"
		}
		maven {
			name "jboss-snapshots"
			url "http://snapshots.jboss.org/maven2/"
		}
	}
	dependencies {
		classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
		classpath 'org.hibernate.build.gradle:gradle-animalSniffer-plugin:1.0.1.Final'
	}
}

apply plugin: 'java'

apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

apply plugin: 'org.hibernate.build.gradle.animalSniffer'

apply plugin: 'idea'

repositories {
	mavenCentral()
	jcenter()
	maven {
		name "jboss-snapshots"
		url "http://snapshots.jboss.org/maven2/"
	}
}

sourceCompatibility = 1.6

group = 'org.hibernate.design.orm'
version = '1.0.0-SNAPSHOT'

configurations {
	provided {
		// todo : need to make sure these are non-exported
		description = 'Non-exported compile-time dependencies.'
	}
}

dependencies {
	compile 'org.jboss.logging:jboss-logging:3.3.0.Final'
	compile 'org.hibernate:hibernate-core:5.1.0.Final'
	compile 'org.hibernate:hibernate-sqm:1.0.0-SNAPSHOT'

	provided 'org.jboss.logging:jboss-logging-annotations:2.0.0.Final'

	testCompile group: 'junit', name: 'junit', version: '4.11'
	testCompile 'org.hibernate:hibernate-testing:5.1.0.Final'

	testRuntime 'com.h2database:h2:1.3.176'
	testRuntime 'log4j:log4j:1.2.17'

	animalSnifferSignature 'org.codehaus.mojo.signature:java16:1.0@signature'
}

task compile
tasks.withType( JavaCompile ).each {
	tasks.compile.dependsOn it
	it.options.compilerArgs += [
			"-nowarn",
			"-encoding", "UTF-8"
	]
}

idea {
	project {
		jdkName = '1.6'
		languageLevel = '1.6'

		vcs = 'Git'
	}
	module {
		jdkName = sourceCompatibility

		excludeDirs = [file( ".gradle" )]
		excludeDirs += file( "$buildDir/classes" )
		excludeDirs += file( "$buildDir/packages" )
		excludeDirs += file( "$buildDir/dependency-cache" )
		excludeDirs += file( "$buildDir/libs" )
		excludeDirs += file( "$buildDir/reports" )
		excludeDirs += file( "$buildDir/resources" )
		excludeDirs += file( "$buildDir/test-results" )
		excludeDirs += file( "$buildDir/tmp" )

		downloadSources = true
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = 2.4
}
